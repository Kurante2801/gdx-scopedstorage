plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

group = 'com.github.kurante2801'
version = "$VERSION_NAME"

android {
    namespace 'kurante.gdxscopedstorage'
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    api "com.badlogicgames.gdx:gdx-backend-android:1.11.0"
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    api 'androidx.documentfile:documentfile:1.0.1'
    api 'androidx.activity:activity-ktx:1.6.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.kurante2801'
                artifactId = 'gdx-scopedstorage'
                version = "$VERSION_NAME"

                pom {
                    name = "libGDX Scoped Storage"
                    description = "Wrapper for Android's Storage Access Framework (SAF)"
                    url = "https://github.com/kurante2801/gdx-scopedstorage"
                    licenses {
                        license {
                            name = 'The MIT License'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'kurante2801'
                            name = 'Kurante'
                            email = 'kurante2801@gmail.com'
                        }
                    }
                    scm {
                        url = "https://github.com/kurante2801/gdx-scopedstorage.git"
                    }
                }
            }
        }
    }
}